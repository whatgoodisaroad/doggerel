# From https://en.wikipedia.org/wiki/Dimensional_analysis

# Setup:

# Qualitative units to represent quantities of nitrogen oxides and other gasses
# it's mixed into.
unit NOx;
unit gas;
unit volume;

# Standard definitions for grams and kilograms.
dim mass;
unit gram of mass;
unit kilogram of mass;
convert gram to kilogram with kilogram = 1000 * gram;

# Moles are arguably dimensionless quantities of ~amount~, but here we'll call
# it in dimensions of amount for convenience.
dim amount;
unit mole of amount;
unit kilamole of amount;
convert mole to kilamole with kilamole = 1000 * mole;

dim length;
unit meter of length;

dim time;
unit minute of time;
unit hour of time;
convert minute to hour with hour = 60 * minute;

# Assign values:
let NOx_concentration = (10 NOx volume) / (1000000 gas volume);
let NOx_molar_mass = 46 gram/mole;
let gas_flow_rate = 20 gas meter^3/minute;
let molar_volume_of_gas = 22.414 meter^3/kilamole;

let mass_flow_rate =
  ((NOx_concentration * NOx_molar_mass) / molar_volume_of_gas) * gas_flow_rate;
print mass_flow_rate as NOx gram/hour;
